
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Метаданные.Имя = "УправлениеНедвижимостьюИАрендойКОРП" Тогда
		 	
		Элементы.ГруппаБП.Видимость = Истина;
		Элементы.Выгрузить.Видимость = Истина;
		Элементы.ФормаЗагрузить.Видимость = Ложь;
				
	ИначеЕсли Метаданные.Имя = "ITILКОРП" Тогда
		
		Элементы.ГруппаБП.Видимость = Ложь;
		Элементы.Выгрузить.Видимость = Ложь;
		Элементы.ФормаЗагрузить.Видимость = Истина;
 
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыгрузитьНаСервере()
	
	ДанныеВыгрузки = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтборПоДатеНачала", ЗначениеЗаполнено(НачалоПериодаВыгрузки));
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(НачалоПериодаВыгрузки));
	Запрос.УстановитьПараметр("ОтборПоДатеОкончания", ЗначениеЗаполнено(КонецПериодаВыгрузки));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(КонецПериодаВыгрузки));
	Запрос.УстановитьПараметр("ОтборПоОтветственному", ЗначениеЗаполнено(Ответственный));
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	Запрос.Текст = "ВЫБРАТЬ
	|	ПТиУ.Ссылка КАК Поступление,
	|	ПТиУ.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ_Поступления
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПТиУ
	|ГДЕ
	|	ПТиУ.Проведен
	|	И НЕ ПТиУ.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА &ОтборПоДатеНачала
	|				ТОГДА ПТиУ.Дата >= &ДатаНачала
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ОтборПоДатеОкончания
	|				ТОГДА ПТиУ.Дата <= &ДатаОкончания
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ОтборПоОтветственному
	|				ТОГДА ПТиУ.Ответственный = &Ответственный
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ВТ_Поступления.Дата, ДЕНЬ) КАК Дата,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура КАК Номенклатура,
	|	СРЕДНЕЕ(ПоступлениеТоваровУслугОборудование.Цена) КАК Цена
	|ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
	|ИЗ
	|	ВТ_Поступления КАК ВТ_Поступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|		ПО ВТ_Поступления.Поступление = ПоступлениеТоваровУслугОборудование.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ВТ_Поступления.Дата, ДЕНЬ),
	|	ПоступлениеТоваровУслугОборудование.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ВТ_Поступления.Дата, ДЕНЬ),
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	СРЕДНЕЕ(ПоступлениеТоваровУслугТовары.Цена)
	|ИЗ
	|	ВТ_Поступления КАК ВТ_Поступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО ВТ_Поступления.Поступление = ПоступлениеТоваровУслугТовары.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ВТ_Поступления.Дата, ДЕНЬ),
	|	ПоступлениеТоваровУслугТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ВТ_Поступления.Дата, ДЕНЬ),
	|	ПоступлениеТоваровУслугУслуги.Номенклатура,
	|	СРЕДНЕЕ(ПоступлениеТоваровУслугУслуги.Цена)
	|ИЗ
	|	ВТ_Поступления КАК ВТ_Поступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|		ПО ВТ_Поступления.Поступление = ПоступлениеТоваровУслугУслуги.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ВТ_Поступления.Дата, ДЕНЬ),
	|	ПоступлениеТоваровУслугУслуги.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЦеныНоменклатуры.Дата КАК ДатаЗакупа,
	|	ВТ_ЦеныНоменклатуры.Номенклатура КАК КонфигурационнаяЕдиница,
	|	СРЕДНЕЕ(ВТ_ЦеныНоменклатуры.Цена) КАК ЦенаЗаЕдиницуБезНДС
	|ИЗ
	|	ВТ_ЦеныНоменклатуры КАК ВТ_ЦеныНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЦеныНоменклатуры.Дата,
	|	ВТ_ЦеныНоменклатуры.Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураДанных = Новый Структура("КонфигурационнаяЕдиница, ДатаЗакупа, ЦенаЗаЕдиницуБезНДС");
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);
		ДанныеВыгрузки.Добавить(СтруктураДанных);
	КонецЦикла;

	Возврат ДанныеВыгрузки;
КонецФункции

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ПутьФайлаВыгрузкиИзБП = "";
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = "";
	Диалог.Заголовок = "Сохраните файл";
	Диалог.Фильтр = "(*.xml)|*.xml|";
	
	Если Диалог.Выбрать() Тогда
		ПутьФайлаВыгрузкиИзБП = Диалог.ПолноеИмяФайла;
		ДанныеДляВыгрузки = ВыгрузитьНаСервере();
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьФайл(ПутьФайлаВыгрузкиИзБП);
		СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ДанныеДляВыгрузки);
		ЗаписьXML.Закрыть();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выгрузка успешно завершена!");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере(МассивДанных, ОшибкаЧтения)
	
	ТаблицаИзМассиваСтруктур = СтруктураВТаблицуЗначенийСОписаниемТипов(МассивДанных);
	КолонкиТаблицы = ТаблицаИзМассиваСтруктур.Колонки;
	Если КолонкиТаблицы.Найти("КонфигурационнаяЕдиница") = Неопределено
		ИЛИ КолонкиТаблицы.Найти("ДатаЗакупа") = Неопределено
		ИЛИ КолонкиТаблицы.Найти("ЦенаЗаЕдиницуБезНДС") = Неопределено Тогда 
		ОшибкаЧтения = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из ТаблицаИзМассиваСтруктур Цикл 
		
		Если Не ЗначениеЗаполнено(Строка.КонфигурационнаяЕдиница) Тогда
			Продолжить;
		КонецЕсли;
		
		ГУИДКЕ = Новый УникальныйИдентификатор(Строка.КонфигурационнаяЕдиница);
		СсылкаКЕ = Справочники.itilprofКонфигурационныеЕдиницы.ПолучитьСсылку(ГУИДКЕ);
		
		Если Не ОбщегоНазначения.СсылкаСуществует(СсылкаКЕ) Тогда
			Продолжить;
		КонецЕсли;
		
		МенЗаписи = РегистрыСведений.пр_ИсторияЗакупаКонфигурационныхЕдиниц.СоздатьМенеджерЗаписи();
		МенЗаписи.КонфигурационнаяЕдиница = СсылкаКЕ;
		МенЗаписи.ДатаЗакупа = Строка.ДатаЗакупа;
		МенЗаписи.ЦенаЗаЕдиницуБезНДС = Строка.ЦенаЗаЕдиницуБезНДС;
		МенЗаписи.Записать(Истина);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ПолныйПутьДоФайлаЗагрузки = "";
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.ПолноеИмяФайла = "";
	Диалог.Заголовок = "Открыть файл";
	Диалог.Фильтр = "(*.xml)|*.xml|";
	
	Если Диалог.Выбрать() Тогда
			
		ПолныйПутьДоФайлаЗагрузки = Диалог.ПолноеИмяФайла;
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ПолныйПутьДоФайлаЗагрузки);               
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(ТекстовыйДокумент.ПолучитьТекст());
		
		ТипОбъектаXDTO = ФабрикаXDTO.Тип("http://v8.1c.ru/8.1/data/core","Array");
		ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML,ТипОбъектаXDTO);
		ОбъектXDTO.Проверить();
		МассивДанных = СериализаторXDTO.ПрочитатьXDTO(ОбъектXDTO);
		ЧтениеXML.Закрыть();
		
		ОшибкаЧтения = Ложь;
		
		ЗагрузитьНаСервере(МассивДанных, ОшибкаЧтения);
		Если ОшибкаЧтения Тогда
			Сообщение = "При чтении файла данных произошли ошибки. Возможно выбран не тот файл.";
		Иначе
			Сообщение = "Загрузка успешно завершена!";
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение);
		
	КонецЕсли;
		
КонецПроцедуры 

Функция СтруктураВТаблицуЗначенийСОписаниемТипов(МассивСтруктур) 
		
	ТаблицаИзМассиваСтруктур = Новый ТаблицаЗначений;
	
	Для Каждого СтруктураДанных Из МассивСтруктур Цикл
		
		Для Каждого КлючИЗначение Из СтруктураДанных Цикл
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Дата") Тогда
				ОпТип = Новый ОписаниеТипов("Дата");
			ИначеЕсли ТипЗнч(КлючИЗначение.Значение) = Тип("Число") Тогда
				ОпТип = Новый ОписаниеТипов("Число");
			ИначеЕсли ТипЗнч(КлючИЗначение.Значение) = Тип("Булево") Тогда 
				ОпТип = Новый ОписаниеТипов("Булево");
			Иначе 
				ДлинаСтроки = СтрДлина(КлючИЗначение.Значение);
				ОпТип = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(ДлинаСтроки, ДопустимаяДлина.Переменная));
			КонецЕсли;
			
			Если ТаблицаИзМассиваСтруктур.Колонки.Найти(КлючИЗначение.Ключ) = Неопределено Тогда
				ТаблицаИзМассиваСтруктур.Колонки.Добавить(КлючИЗначение.Ключ, ОпТип);	
			КонецЕсли;
			
		КонецЦикла;
		
		НоваяСтрока = ТаблицаИзМассиваСтруктур.Добавить();
		
		Для Каждого КлючИЗначение Из СтруктураДанных Цикл	
			НоваяСтрока[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
		КонецЦикла;
		
	КонецЦикла;
	Возврат ТаблицаИзМассиваСтруктур;
	
КонецФункции
